mapscripts OldaleTown_Mart_MapScripts {}

script OldaleTown_Mart_EventScript_Employee {
	lock
	faceplayer
	message(Text_HowMayIServeYou)
	waitmessage
	if (flag(FLAG_ADVENTURE_STARTED)) {
		callnative(StoreBadgeCountAsResult)
		if (var(VAR_RESULT) < 6) {
			pokemart(OldaleTown_Mart_Items_AfterAdventureStarted)
		} else {
			pokemart(OldaleTown_Mart_Items_SixBadges)
		}
	} else {
		pokemart(OldaleTown_Mart_Items_BeforeAdventureStarted)
	}
	msgbox(Text_PleaseComeAgain)
	release
}

mart OldaleTown_Mart_Items_BeforeAdventureStarted {
	ITEM_POTION
	ITEM_ANTIDOTE
	ITEM_PARALYZE_HEAL
	ITEM_AWAKENING
}

mart OldaleTown_Mart_Items_AfterAdventureStarted {
	ITEM_POKE_BALL
	ITEM_POTION
	ITEM_ANTIDOTE
	ITEM_PARALYZE_HEAL
	ITEM_AWAKENING
}

mart OldaleTown_Mart_Items_SixBadges {
	ITEM_POKE_BALL
	ITEM_GREAT_BALL
	ITEM_ULTRA_BALL
	ITEM_POTION
	ITEM_HYPER_POTION
	ITEM_MAX_POTION
	ITEM_ANTIDOTE
	ITEM_PARALYZE_HEAL
	ITEM_AWAKENING
	ITEM_FULL_RESTORE
	ITEM_REVIVE
	ITEM_FULL_HEAL
	ITEM_ESCAPE_ROPE
	ITEM_REPEL
	ITEM_SUPER_REPEL
	ITEM_MAX_REPEL
}

script OldaleTown_Mart_EventScript_Woman {
	lock
	faceplayer
	if (flag(FLAG_ADVENTURE_STARTED)) {
		msgbox(OldaleTown_Mart_Text_174ACB)
	} else {
		msgbox(OldaleTown_Mart_Text_174A8A)
	}
	release
}

script OldaleTown_Mart_EventScript_Boy {
	msgbox(OldaleTown_Mart_Text_174B10, MSGBOX_NPC)
}

// TODO move these to a common file
script OldaleTown_Mart_EventScript_MasterBallMerchant {
	lock
	faceplayer
	if (!flag(FLAG_MET_MASTERBALL_MERCHANTS)) {
		msgbox(OldaleTown_Mart_Text_MasterBallMerchant_FirstTime, MSGBOX_NPC)
		setflag(FLAG_MET_MASTERBALL_MERCHANTS)
	}
	checkmoney(80000, 0)
	if (var(VAR_RESULT)) {
		showmoneybox(0, 0)
		msgbox(OldaleTown_Mart_Text_MasterBallMerchant_HaveSufficientMoney, MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			call(OldaleTown_Mart_WouldLikeMasterBall)
		}
		hidemoneybox(0, 0)
	} else {
		msgbox(OldaleTown_Mart_Text_MasterBallMerchant_DoNotHaveSufficientMoney, MSGBOX_NPC)
	}
	release
}

script OldaleTown_Mart_WouldLikeMasterBall {
	checkitemspace(ITEM_MASTER_BALL, 1)
	if (var(VAR_RESULT)) {
		removemoney(80000, 0)
		updatemoneybox(0, 0)
	}
	giveitem(ITEM_MASTER_BALL)
	if (!var(VAR_RESULT)) {
		msgbox(Text_TooBadBagIsFull, MSGBOX_DEFAULT)
	}
}

text OldaleTown_Mart_Text_MasterBallMerchant_FirstTime {
	format("Oh! Are you {PLAYER}?\p"
		"I heard you have all eight gym badges! That means that you're eligible to buy Master Balls.\p"
		"I should warn you though - they are not cheap.")
}

text OldaleTown_Mart_Text_MasterBallMerchant_HaveSufficientMoney {
	format("Would you like to buy a Master Ball? It costs ¥80,000.")
}

text OldaleTown_Mart_Text_MasterBallMerchant_DoNotHaveSufficientMoney {
	format("You need ¥80,000 to buy a Master Ball.\p"
		"Please come back when you have enough!")
}
