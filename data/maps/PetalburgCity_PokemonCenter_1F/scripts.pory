mapscripts PetalburgCity_PokemonCenter_1F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION: PetalburgCity_PokemonCenter_1F_MapScript1
}

script PetalburgCity_PokemonCenter_1F_MapScript1 {
	setrespawn(HEAL_LOCATION_PETALBURG_CITY)
	call(Common_EventScript_UpdateBrineyLocation)
}

script PetalburgCity_PokemonCenter_1F_EventScript_PkmnCenterNurse {
	setvar(VAR_0x800B, 1)
	call(Common_EventScript_PkmnCenterNurse)
	waitmessage
	waitbuttonpress
	release
}

script PetalburgCity_PokemonCenter_1F_EventScript_FatMan {
	msgbox(PetalburgCity_PokemonCenter_1F_Text_17BD30, MSGBOX_NPC)
}

script PetalburgCity_PokemonCenter_1F_EventScript_Youngster {
	msgbox(PetalburgCity_PokemonCenter_1F_Text_17BD92, MSGBOX_NPC)
}

script PetalburgCity_PokemonCenter_1F_EventScript_Woman {
	lock
	faceplayer
	if (var(VAR_KANTO_STARTERS_STATE) > 0) {
		call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_AfterBattle)
		release
		end
	}
	msgbox(PetalburgCity_PokemonCenter_1F_Text_Woman_BeforeBattle)
	bufferstring(0, PetalburgCity_PokemonCenter_1F_Text_Woman_BeforeBattle_BattleChoice)
	call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_OfferChoice)
	trainerbattle_no_intro(TRAINER_BIANCA, PetalburgCity_PokemonCenter_1F_Text_Woman_WonBattle)
	msgbox(PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_Preamble)
	setvar(VAR_KANTO_STARTERS_STATE, 1)
	call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_AfterBattle)
	release
}

script PetalburgCity_PokemonCenter_1F_EventScript_Woman_AfterBattle {
	if (var(VAR_KANTO_STARTERS_STATE) == 1) {
		bufferstring(0, PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceiveBulbasaur)
		setvar(VAR_TEMP_0, SPECIES_BULBASAUR)
		call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_TryGiveMon)
	}
	if (var(VAR_KANTO_STARTERS_STATE) == 2) {
		bufferstring(0, PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceiveCharmander)
		setvar(VAR_TEMP_0, SPECIES_CHARMANDER)
		call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_TryGiveMon)
	}
	if (var(VAR_KANTO_STARTERS_STATE) == 3) {
		bufferstring(0, PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceiveSquirtle)
		setvar(VAR_TEMP_0, SPECIES_SQUIRTLE)
		call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_TryGiveMon)
	}
	if (var(VAR_KANTO_STARTERS_STATE) == 4) {
		msgbox(PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_AllPokemonReceived)
	}
}

script PetalburgCity_PokemonCenter_1F_EventScript_Woman_TryGiveMon {
	call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_CheckPartySize)
	call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_OfferChoice)
	playfanfare(MUS_OBTAIN_ITEM)
	bufferspeciesname(1, VAR_TEMP_0)
	message(PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceivedPokemon)
	waitfanfare
	waitmessage
	givemon(VAR_TEMP_0, 5, ITEM_NONE)
	call(PetalburgCity_PokemonCenter_1F_EventScript_Woman_OfferToNicknamePokemon)
	addvar(VAR_KANTO_STARTERS_STATE, 1)
}

script PetalburgCity_PokemonCenter_1F_EventScript_Woman_OfferChoice {
	msgbox(gStringVar1, MSGBOX_YESNO)
	if (var(VAR_RESULT) == 0) {
		msgbox(PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_DeclinedBattleOrPokemon)
		release
		end
	}
}

script PetalburgCity_PokemonCenter_1F_EventScript_Woman_CheckPartySize {
	getpartysize
	if (var(VAR_RESULT) == 6) {
		msgbox(PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_NotEnoughSpace)
		release
		end
	}
	copyvar(0x8004, VAR_RESULT)
}

script PetalburgCity_PokemonCenter_1F_EventScript_Woman_OfferToNicknamePokemon {
	msgbox(PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_GivePokemonNickname, MSGBOX_YESNO)
	if (var(VAR_RESULT) == 1) {
		call(Common_EventScript_NameReceivedPartyMon)
		lock
	}
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_BeforeBattle {
	format("I just got back from the Kanto region.\p"
	       "I acquired some rare POKéMON while I was there.\p"
		   "You look like you'd be interested to see them.")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_BeforeBattle_BattleChoice {
	format("Care for a battle?")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_WonBattle {
	format("Impressive.")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_Preamble {
	format("Very well done!\p"
	       "I've been looking for a trainer who can look after these POKéMON and you seem up to the responsibility.")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_NotEnoughSpace {
	format("Oh, it seems you don't have space in your party.\p"
	       "Feel free to come back when you have space for this POKéMON.")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_DeclinedBattleOrPokemon {
	format("Okay, feel free to come back if you change your mind.")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceivedPokemon {
	format("{PLAYER} received {STR_VAR_2}.")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceiveBulbasaur {
	format("Would you like to take BULBASAUR?")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceiveCharmander {
	format("Would you like to take CHARMANDER?")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_ReceiveSquirtle {
	format("Would you like to take SQUIRTLE?")
}

text PetalburgCity_PokemonCenter_1F_Text_Woman_AfterBattle_AllPokemonReceived {
	format("Take good care of those POKéMON.")
}
